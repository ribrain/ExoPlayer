// Copyright (C) 2017 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

apply plugin: 'maven-publish'

if (project.ext.has("exoplayerPublishEnabled")
        && project.ext.exoplayerPublishEnabled) {
    apply plugin: 'bintray-release'
    publish {
        artifactId = releaseArtifact
        description = releaseDescription
        version = releaseVersion
        repoName = getBintrayRepo()
        userOrg = 'google'
        groupId = 'com.google.android.exoplayer'
        website = 'https://github.com/google/ExoPlayer'
    }
}

def getBintrayRepo() {
    boolean publicRepo = hasProperty('publicRepo') &&
        property('publicRepo').toBoolean()
    return publicRepo ? 'exoplayer' : 'exoplayer-test'
}

publishing {
    repositories {
        maven {
            url System.getenv("S3_MAVEN_BUCKET")
            credentials(AwsCredentials) {
                accessKey System.getenv("S3_ACCESS_KEY")
                secretKey System.getenv("S3_SECRET_KEY")
            }
        }
    }
}